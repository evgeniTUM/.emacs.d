* Initialization
** Inits
   #+BEGIN_SRC emacs-lisp
(add-to-list 'load-path "~/.emacs.d/lisp/")
(require 'cl)
(setq user-full-name "Evgeni Pavlidis")
   #+END_SRC

** Machine specific settings
  #+BEGIN_SRC emacs-lisp
    (when (string= system-name "evgeni-ThinkPad-X300")
      )
  #+END_SRC

** Encoding
#+begin_src emacs-lisp
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (set-language-environment "UTF-8")
  (prefer-coding-system 'utf-8)
#+end_src
 
** el-get
#+begin_src emacs-lisp
(add-to-list 'load-path "~/.emacs.d/el-get/el-get")

(unless (require 'el-get nil 'noerror)
  (with-current-buffer
      (url-retrieve-synchronously
       "https://raw.github.com/dimitri/el-get/master/el-get-install.el")
    (goto-char (point-max))
    (eval-print-last-sexp)))

(add-to-list 'el-get-recipe-path "~/.emacs.d/el-get-user/recipes")
(el-get 'sync)
#+end_src

* Auto-complete
** General
  #+BEGIN_SRC emacs-lisp
(require 'auto-complete-config)
(ac-config-default)

(ac-flyspell-workaround)

(setq-default ac-sources '(ac-source-yasnippet
                           ac-source-semantic 
                           ac-source-semantic-raw
                           ac-source-abbrev
                           ac-source-dictionary
                           ac-source-words-in-same-mode-buffers))

(define-key ac-completing-map [return] nil)
(define-key ac-completing-map "\r" nil)
(define-key ac-completing-map (kbd "M-TAB") nil)

(define-key ac-completing-map [tab] 'auto-complete)
(define-key ac-completing-map "\t" 'auto-complete)
  #+end_src

** Menu bindings
  #+begin_src emacs-lisp
(define-key ac-menu-map [tab] 'ac-expand-common)
(define-key ac-menu-map "\t" 'ac-expand-common)

(define-key ac-menu-map [return] 'ac-complete)
(define-key ac-menu-map "\r" 'ac-complete)
(define-key ac-menu-map (kbd "C-n") 'ac-next)
(define-key ac-menu-map (kbd "C-p") 'ac-previous)    
(define-key ac-menu-map (kbd "C-h") 'ac-quick-help)
  #+END_SRC

** fuzzy isearch
hack popup-isearch to do fuzzy search with the help of grizzl
#+begin_src emacs-lisp
;; (defvar *evgeni/isearch-cache* nil)

;; (defun popup-isearch-filter-list (pattern list)
;;   (let*  ((index (grizzl-make-index list))
;;           (pattern (replace-regexp-in-string " " "" pattern))
;;           (result (grizzl-search pattern index *evgeni/isearch-cache*)))
;;     (grizzl-result-strings result index)))
#+end_src

** Sources
*** ac-math 
    #+BEGIN_SRC emacs-lisp
(require 'ac-math) 
(add-to-list 'ac-modes 'latex-mode) 

 (defun ac-LaTeX-mode-setup () ; add ac-sources to default ac-sources
   (setq ac-sources
         (append '(ac-source-math-unicode ac-source-math-latex ac-source-latex-commands)
                 ac-sources))
   )
(add-hook 'LaTeX-mode-hook 'ac-LaTeX-mode-setup)
(setq ac-math-unicode-in-math-p t)
    #+END_SRC

*** ac-slime
   #+BEGIN_SRC emacs-lisp
(add-hook 'slime-mode-hook 'set-up-slime-ac)
(add-hook 'slime-repl-mode-hook 'set-up-slime-ac)
(eval-after-load "auto-complete"
  '(add-to-list 'ac-modes 'slime-repl-mode))
   #+END_SRC

*** ac-pcomplete
   #+BEGIN_SRC emacs-lisp
     (defun ac-pcomplete ()
       ;; eshell uses `insert-and-inherit' to insert a \t if no completion
       ;; can be found, but this must not happen as auto-complete source
       (flet ((insert-and-inherit (&rest args)))
         ;; this code is stolen from `pcomplete' in pcomplete.el
         (let* (tramp-mode ;; do not automatically complete remote stuff
                (pcomplete-stub)
                (pcomplete-show-list t) ;; inhibit patterns like * being deleted
                pcomplete-seen pcomplete-norm-func
                pcomplete-args pcomplete-last pcomplete-index
                (pcomplete-autolist pcomplete-autolist)
                (pcomplete-suffix-list pcomplete-suffix-list)
                (candidates (pcomplete-completions))
                (beg (pcomplete-begin))
                ;; note, buffer text and completion argument may be
                ;; different because the buffer text may bet transformed
                ;; before being completed (e.g. variables like $HOME may be
                ;; expanded)
                (buftext (buffer-substring beg (point)))
                (arg (nth pcomplete-index pcomplete-args)))
           ;; we auto-complete only if the stub is non-empty and matches
           ;; the end of the buffer text
           (when (and (not (zerop (length pcomplete-stub)))
                      (or (string= pcomplete-stub ; Emacs 23
                                   (substring buftext
                                              (max 0
                                                   (- (length buftext)
                                                      (length pcomplete-stub)))))
                          (string= pcomplete-stub ; Emacs 24
                                   (substring arg
                                              (max 0
                                                   (- (length arg)
                                                      (length pcomplete-stub)))))))
             ;; Collect all possible completions for the stub. Note that
             ;; `candidates` may be a function, that's why we use
             ;; `all-completions`.
             (let* ((cnds (all-completions pcomplete-stub candidates))
                    (bnds (completion-boundaries pcomplete-stub
                                                 candidates
                                                 nil
                                                 ""))
                    (skip (- (length pcomplete-stub) (car bnds))))
               ;; We replace the stub at the beginning of each candidate by
               ;; the real buffer content.
               (mapcar #'(lambda (cand) (concat buftext (substring cand skip)))
                       cnds))))))
     
     (defvar ac-source-pcomplete
       '((candidates . ac-pcomplete)))
   #+END_SRC

*** ac-shell
    #+BEGIN_SRC emacs-lisp :tangle no
(setq explicit-shell-file-name "bash")
(setq explicit-bash-args '("-c" "export EMACS=; stty echo; bash"))
(setq comint-process-echoes t)
(require 'readline-complete)
(add-to-list 'ac-modes 'shell-mode)
(add-hook 'shell-mode-hook 'ac-rlc-setup-sources)
    #+END_SRC

*** ac-eshell
   #+BEGIN_SRC emacs-lisp
     (add-hook 'eshell-mode-hook #'(lambda () (setq ac-sources '(ac-source-pcomplete))))
     (add-to-list 'ac-modes 'eshell-mode)
   #+END_SRC

*** ac-ielm (interactive emacs lisp mode)
   #+BEGIN_SRC emacs-lisp
     (defun ielm-auto-complete ()
       "Enables `auto-complete' support in \\[ielm]."
       (setq ac-sources '(ac-source-functions
                          ac-source-variables
                          ac-source-features
                          ac-source-symbols
                          ac-source-words-in-same-mode-buffers))
       (add-to-list 'ac-modes 'inferior-emacs-lisp-mode)
       (auto-complete-mode 1))
     (add-hook 'ielm-mode-hook 'ielm-auto-complete)
   #+END_SRC

*** org-ac
#+begin_src emacs-lisp
(require 'org-ac)
(org-ac/config-default)
#+end_src

* ORG mode
** General
  #+BEGIN_SRC emacs-lisp
    (setq org-src-fontify-natively t)
    (setq org-confirm-babel-evaluate nil)
    (add-hook 'org-mode-hook 
              '(lambda () 
                 (flyspell-mode)
                 (local-unset-key (kbd "C-c SPC"))
                 (org-indent-mode)))
    
    (setq org-completion-use-iswitchb t)
    (setq org-export-babel-evaluate 'inline-only)
  #+END_SRC

** integrate ebib
#+begin_src emacs-lisp
(org-add-link-type "ebib" 'ebib)
(setq ebib-preload-bib-files '("~/thesis/bibliography.bib"))

(org-add-link-type 
 "ebib" 'ebib
 (lambda (path desc format)
   (cond
    ((eq format 'html)
     (format "(<cite>%s</cite>)" path))
    ((eq format 'latex)
     (if (or (not desc) (equal 0 (search "cite:" desc)))
         (format "\\cite{%s}" path)
       (format "\\cite[%s][%s]{%s}"
               (cadr (split-string desc ";"))
               (car (split-string desc ";"))  path))))))
#+end_src

** LaTeX
*** Preview
#+begin_src emacs-lisp
(setq org-format-latex-options (plist-put org-format-latex-options :scale 1.5))
#+end_src

*** Xelatex
http://joat-programmer.blogspot.de/2013/07/org-mode-version-8-and-pdf-export-with.html
and 
http://orgmode.org/worg/org-faq.html#using-xelatex-for-pdf-export

#+begin_src emacs-lisp
  (require 'ox-latex)
  (setq org-export-latex-listings t)
  (setq org-latex-pdf-process 
        (list "latexmk -bibtex -pdflatex=xelatex -pdf -quiet  -f  %f"))

  (setq org-export-latex-default-packages-alist
        '(("" "fontspec" t)
          ("" "xunicode" t)
          ("" "url" t)
          ("" "rotating" t)
          ("american" "babel" t)
          ("babel" "csquotes" t)
          ("" "soul" t)
          ("xetex" "hyperref" nil)
          ))
#+end_src

*** RefTex integration
#+begin_src emacs-lisp
  (defun evgeni/org-mode-setup ()
    (when (and (buffer-file-name)
               (file-exists-p (buffer-file-name)))
      (load-library "reftex")
      (and (buffer-file-name)
           (file-exists-p (buffer-file-name))
           (reftex-parse-all))))
  (add-hook 'org-mode-hook 'evgeni/org-mode-setup)
#+end_src

*** Scrbook class
#+begin_src emacs-lisp
  (require 'ox-latex)
  (unless (find "scrbook" org-latex-classes :key 'car
                :test 'equal)
    (add-to-list 'org-latex-classes
                 '("scrbook" "\\documentclass{scrbook}
                  [NO-DEFAULT-PACKAGES]
                   [EXTRA]"
                   ("\\chapter{%s}". "\\chapter*{%s}")
                   ("\\section{%s}" . "\\section*{%s}")
                   ("\\subsection{%s}" . "\\subsection*{%s}")
                   ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                   ("\\paragraph{%s}" . "\\paragraph*{%s}")
                   ("\\subparagraph{%s}" . "\\subparagraph*{%s}"))))
#+end_src

*** Don't export some headings
#+begin_src emacs-lisp
(defun evgeni/export-delete-headlines-tagged-noheading (backend)
  (dolist (hl (nreverse (org-element-map (org-element-parse-buffer 'headline)
                                         'headline
                                         'identity)))
    (when (member "noheading" (org-element-property :tags hl))
      (goto-char (org-element-property :begin hl))
      (delete-region (point) (progn (forward-line) (point))))))

(add-to-list 'org-export-before-processing-hook
             'evgeni/export-delete-headlines-tagged-noheading)
#+end_src

** Babel
  #+BEGIN_SRC emacs-lisp
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((R . t)
       (emacs-lisp . t)
       (python . t)
       (dot . t)
       (ditaa . t)
       (gnuplot . t)
       (latex . t)
       (lisp . t)
       (maxima . t)
       (octave .t)
       (sh . t)
       (plantuml . t)))
  #+END_SRC

*** Python
#+begin_src emacs-lisp
(defadvice org-babel-python-evaluate-session
  (around org-python-use-cpaste
          (session body &optional result-type result-params) activate)
  "add a %cpaste and '--' to the body, so that ipython does the right
thing."
  (setq body (concat "%cpaste\n" body "\n--"))
  ad-do-it  )
#+end_src

*** PlantUML
    #+BEGIN_SRC emacs-lisp
      (setq org-plantuml-jar-path
            (expand-file-name "~/.emacs.d/lib/plantuml.jar"))
    #+END_SRC

*** Ditaa
#+begin_src emacs-lisp
     (setq org-ditaa-jar-path "/usr/share/ditaa/ditaa.jar")
#+end_src

* Helm
#+BEGIN_SRC emacs-lisp
(helm-mode 1) 
#+END_SRC

** ac-helm
#+begin_src emacs-lisp
(require 'ac-helm)
(defun evgeni/ac-complete-with-helm ()
  "Select `auto-complete' candidates by `helm'.
It is useful to narrow candidates."
  (interactive)
  (unless ac-completing
    (call-interactively 'auto-complete)
    (helm-other-buffer '(helm-source-auto-complete-candidates)
                       "*helm auto-complete*")))
#+end_src

** helm-ag
#+begin_src emacs-lisp
(setq helm-ag-source-type 'file-line)
#+end_src

** helm-bibtex
#+begin_src emacs-lisp
(setq helm-bibtex-bibliography "~/thesis/bibliography.bib")

(defadvice helm-bibtex-open-pdf (around evgeni/helm-bibtex-open-pdf)
  "Open the PDF associated with the entry using the function
specified in `helm-bibtex-pdf-open-function',"
  (let ((keys (helm-marked-candidates :with-wildcard t)))
    (dolist (key keys)
      (let* ((entry (helm-bibtex-get-entry key))
             (file (helm-bibtex-get-value entry 'file)))
        (if file (shell-command (concat "evince `find ~/Dropbox/configs/zotero/storage -name " (first (split-string file ":")) "`"))
          (message "No URL or DOI found for this entry: %s"
                   key))))))

#+end_src

** helm-dash
#+begin_src emacs-lisp
(setq helm-dash-docsets-path (expand-file-name "~/.emacs.d/.docsets"))
#+end_src

*** Macros
#+begin_src emacs-lisp
(defun evgeni/dash-docset-installed-p (docset)
  (file-exists-p (concat helm-dash-docsets-path "/" docset ".docset")))

(defun evgeni/dash-install-docsets (docsets)
  (mapcar (lambda (docset) 
            "install docsets if not already installed"
            (unless (evgeni/dash-docset-installed-p docset)
              (helm-dash-install-docset (replace-regexp-in-string " " "_" docset))))
          docsets))

(defmacro evgeni/dash-define-docsets-for-mode (hook docsets)
  `(progn
     (evgeni/dash-install-docsets ,docsets)
     (add-hook ,hook '(lambda () 
                        "load dashsets for this mode"
                        (setq-local helm-dash-docsets ,docsets)))))
#+end_src

*** Python
#+begin_src emacs-lisp
(evgeni/dash-define-docsets-for-mode 'python-mode-hook '("Python 2" "NumPy" "SciPy"))
#+end_src

*** Bash
#+begin_src emacs-lisp
(evgeni/dash-define-docsets-for-mode 'shell-script-mode-hook '("Bash"))
#+end_src

*** Emacs lisp
#+begin_src emacs-lisp
(evgeni/dash-define-docsets-for-mode 'emacs-lisp-mode-hook '("Emacs Lisp"))
#+end_src

*** JavaScript
#+begin_src emacs-lisp
;(evgeni/dash-define-docsets-for-mode 'javascript-mode-hook '("JavaScript"))
#+end_src

* Development
** Common
*** smartparens
#+begin_src emacs-lisp
(require 'smartparens-config)
(smartparens-global-mode t)
(show-smartparens-global-mode t)

;;; tex-mode latex-mode
(sp-with-modes '(tex-mode plain-tex-mode latex-mode)
  (sp-local-tag "i" "\"<" "\">"))

;;; html-mode
(sp-with-modes '(html-mode sgml-mode)
  (sp-local-pair "<" ">"))

;;; lisp modes
(sp-with-modes sp--lisp-modes
    (sp-local-pair "(" nil :bind "C-("))
#+end_src

#+begin_src emacs-lisp
;; turn on smartparens-strict-mode on all lisp-like mode
  (dolist (sp--lisp-mode-hook
           (mapcar (lambda (x)
                     (intern (concat (symbol-name x) "-hook")))
                   sp--lisp-modes))
    (add-hook sp--lisp-mode-hook
              'smartparens-strict-mode)
    ;; inferior-emacs-lisp-mode-hook is an alias of ielm-mode-hook
    ;; and it will be overrided when you first start ielm
    (add-hook 'ielm-mode-hook
              'smartparens-strict-mode))
#+end_src

*** yasnippets
   #+BEGIN_SRC emacs-lisp
(require 'yasnippet)
(yas-global-mode 1)
(define-key yas-minor-mode-map (kbd "<tab>") nil)
(define-key yas-minor-mode-map (kbd "TAB") nil)
(define-key yas-keymap (kbd "C-o") 'yas-next-field-or-maybe-expand)
   #+END_SRC

*** eldoc
#+begin_src emacs-lisp
(add-hook 'emacs-lisp-mode-hook 'turn-on-eldoc-mode)
(add-hook 'ielm-mode-hook 'turn-on-eldoc-mode)
#+end_src

** CEDET
#+begin_src emacs-lisp
(setq semantic-default-submodes '(global-semantic-idle-scheduler-mode
                                  global-semanticdb-minor-mode
                                  global-semantic-idle-summary-mode
                                  global-semantic-mru-bookmark-mode))
(semantic-mode 1)
#+end_src

** Lisp
*** eval-sexp-fu
#+begin_src emacs-lisp
(require 'eval-sexp-fu)
#+end_src

*** rosemacs
   #+begin_SRC emacs-lisp
(defun evgeni/ros-slime ()
  (interactive)
  (add-to-list 'load-path (expand-file-name "~/.emacs.d/lisp/rosemacs"))
  (require 'rosemacs nil t)
  (invoke-rosemacs)
  (slime-setup '(slime-fancy slime-asdf slime-ros)))
   #+END_SRC

*** slime
  #+BEGIN_SRC emacs-lisp
    (setq inferior-lisp-program "/usr/bin/sbcl") 
    (slime-setup '(slime-fancy slime-asdf))
    
    (when (file-exists-p (expand-file-name "~/quicklisp/slime-helper.el"))
      (load (expand-file-name "~/quicklisp/slime-helper.el")))
  #+END_SRC

** Python
https://github.com/xiaohanyu/oh-evgeni/emacs/blob/master/modules/ome-python.org
#+BEGIN_SRC emacs-lisp
(setq
 python-shell-interpreter "~/shared/bin/ipython.sh"
 python-shell-interpreter-args ""
 python-shell-prompt-regexp "In \\[[0-9]+\\]: "
 python-shell-prompt-output-regexp "Out\\[[0-9]+\\]: "
 python-shell-completion-setup-code
 "from IPython.core.completerlib import module_completion"
 python-shell-completion-module-string-code
 "';'.join(module_completion('''%s'''))\n"
 python-shell-completion-string-code
 "';'.join(get_ipython().Completer.all_completions('''%s'''))\n")
#+END_SRC

*** jedi
#+begin_src emacs-lisp

(defun evgeni/setup-jedi () 
  (interactive)
  (jedi:setup)
  (define-key jedi-mode-map (kbd "<C-tab>") nil)
  (setq jedi:complete-on-dot t))

(add-hook 'python-mode-hook 'evgeni/setup-jedi)
(add-hook 'inferior-python-mode-hook 'evgeni/setup-jedi)
#+end_src

*** jedi-direx
#+begin_src emacs-lisp
(eval-after-load "python"
  '(define-key python-mode-map "\C-zx" 'jedi-direx:pop-to-buffer))
;(add-hook 'jedi-mode-hook 'jedi-direx:setup)
#+end_src

** Maxima
  #+BEGIN_SRC emacs-lisp
(add-to-list 'load-path "/usr/share/emacs/site-lisp/maxima/")
(autoload 'maxima-mode "maxima" "Maxima mode" t)
(autoload 'imaxima "imaxima" "Frontend for maxima with Image support" t)
(autoload 'maxima "maxima" "Maxima interaction" t)
(autoload 'imath-mode "imath" "Imath mode for math formula input" t)
(setq imaxima-use-maxima-mode-flag t)
  #+END_SRC

** C++
#+begin_src emacs-lisp
(add-hook 'c-mode-hook 'c-turn-on-eldoc-mode)
(add-hook 'c++-mode-hook 'c-turn-on-eldoc-mode)
#+end_src

** Java
#+begin_src emacs-lisp
  (require 'malabar-mode)
  (setq malabar-groovy-lib-dir 
        (expand-file-name "~/.emacs.d/lib/malabar-mode-jar/target/lib"))
  (add-to-list 'auto-mode-alist '("\\.java\\'" . malabar-mode))
#+end_src

** Web Development
*** CSS
#+begin_src emacs-lisp
(add-hook 'css-mode 'turn-on-css-eldoc)
#+end_src

** Other
*** diff-hl
http://steckerhalter.co.vu/steckemacs.html#sec-2-11-8
#+begin_src emacs-lisp
  (global-diff-hl-mode)
  (diff-hl-margin-mode)
  
  (defun evgeni/diff-hl-update ()
    (with-current-buffer (current-buffer) (diff-hl-update)))
  
  (add-hook 'magit-refresh-file-buffer-hook 'evgeni/diff-hl-update)
#+end_src

* Scientific writing
** Textlint
check scientific writing by rules

** Grammar check
needs link-grammar installed 

   #+BEGIN_SRC emacs-lisp
     (defun evgeni/grammar-mode ()
       (interactive)
       (unless (fboundp 'grammar-mode)
         (add-to-list 'load-path (expand-file-name "~/.emacs.d/lisp/grammar"))
         (require 'grammar)
         (setq grammar-program-name (expand-file-name "~/.emacs.d/lisp/grammar/grammar")))
       (command-execute 'grammar-mode))
     
   #+END_SRC

** Synonyms
   #+BEGIN_SRC emacs-lisp
(require 'synonyms)

(setq synonyms-file (expand-file-name "~/.emacs.d/lib/thesaurus/mthesaur.txt"))
(setq synonyms-cache-file (expand-file-name "~/.emacs.d/lib/thesaurus/syn.cache"))
(setq synonyms-match-more-flag nil)
   #+END_SRC
** AucTex
  #+BEGIN_SRC emacs-lisp
(setq TeX-auto-save t)
(setq TeX-parse-self t)

(add-hook 'LaTeX-mode-hook 'visual-line-mode)
(add-hook 'LaTeX-mode-hook 'flyspell-mode)

(add-hook 'LaTeX-mode-hook 'turn-on-reftex)
(setq reftex-plug-into-AUCTeX t)
(add-to-list 'auto-mode-alist '("\\.tex\\'" . latex-mode))

(add-hook 'LaTeX-mode-hook (lambda ()
                             (TeX-fold-mode 1)
			     (setq TeX-PDF-mode t)))

(setq TeX-electric-sub-and-superscript t)
  #+END_SRC

*** Viewer
  #+BEGIN_SRC emacs-lisp

(setq TeX-view-program-list '(("Evince" "evince --page-index=%(outpage) %o")))
(setq TeX-view-program-selection '((output-pdf "Evince")))
(add-hook 'LaTeX-mode-hook 'TeX-source-correlate-mode)
  #+END_SRC

* Environment
** Interface
*** highlight-parentheses
   #+BEGIN_SRC emacs-lisp
(define-globalized-minor-mode global-highlight-parentheses-mode
  highlight-parentheses-mode
  (lambda ()
    (highlight-parentheses-mode t)))
(global-highlight-parentheses-mode t)
   #+END_SRC

*** sublimity
#+begin_src emacs-lisp
(require 'sublimity-map)
(sublimity-map-set-delay 'inf)
#+end_src

*** anzu
#+begin_src emacs-lisp
(require 'anzu)
(global-anzu-mode 1)
#+end_src

*** yascroll
#+begin_src emacs-lisp
(global-yascroll-bar-mode 1)
#+end_src

** Window management
*** spaces
#+begin_src emacs-lisp
  (setq helm-spaces-new-space-query nil)
#+end_src

*** window-number
#+begin_src emacs-lisp
  (require 'window-number)
  (window-number-meta-mode)
#+end_src


integrate golden-ration with window-number

#+begin_src emacs-lisp
(defadvice window-number-select (after window-number-golden-ration-integration activate)
  (when golden-ratio-mode (golden-ratio)))
#+end_src

*** transpose-frame
#+begin_src emacs-lisp
(require 'transpose-frame)
#+end_src

*** dedicated
#+begin_src emacs-lisp
(require 'dedicated)
#+end_src

** Navigation
*** zap-up-to-char
#+begin_src emacs-lisp
(autoload 'zap-up-to-char "misc"
  "Kill up to, but not including ARGth occurrence of CHAR.")
#+end_src

*** back-button
#+begin_src emacs-lisp
(require 'back-button)
(back-button-mode 1)
#+end_src

** save-packages
#+begin_src emacs-lisp
(add-hook 'kill-emacs-hook 'save-packages)
#+end_src

** keyfreq
#+begin_src emacs-lisp
  (require 'keyfreq)
  (keyfreq-mode 1)
  (keyfreq-autosave-mode 1)
#+end_src

** midnight
#+begin_src emacs-lisp
(require 'midnight)
#+end_src

** guide-key
#+begin_src emacs-lisp
  (setq guide-key/guide-key-sequence '("C-x" "C-c" "M-s" "C-h"
                                       (org-mode "C-c C-x")))
  (guide-key-mode 1) 
  (setq guide-key/idle-delay 1)
  (setq guide-key/popup-window-position 'bottom)
  (setq guide-key/recursive-key-sequence-flag t)
#+end_src

** bm (visual bookmarks)
#+begin_src emacs-lisp
  (setq-default bm-buffer-persistence nil)
  
  (setq bm-restore-repository-on-load t)
  (require 'bm)
  (add-hook' after-init-hook 'bm-repository-load)
  (add-hook 'find-file-hooks 'bm-buffer-restore)
  (add-hook 'kill-buffer-hook 'bm-buffer-save)
  (add-hook 'kill-emacs-hook '(lambda nil
                                (bm-buffer-save-all)
                                (bm-repository-save)))
  (add-hook 'after-save-hook 'bm-buffer-save)
  (add-hook 'after-revert-hook 'bm-buffer-restore)
  (add-hook 'vc-before-checkin-hook 'bm-buffer-save)
#+end_src

* Tools
** magit
remap magit keys because of window-meta-mode
#+begin_src emacs-lisp
(add-hook 'magit-mode-hook
          (lambda ()
            (define-key magit-mode-map (kbd "C-c 1") 'magit-show-level-1-all)
            (define-key magit-mode-map (kbd "C-c 2") 'magit-show-level-2-all)
            (define-key magit-mode-map (kbd "C-c 3") 'magit-show-level-3-all)
            (define-key magit-mode-map (kbd "C-c 4") 'magit-show-level-4-all)))
#+end_src

** projectile
#+begin_src emacs-lisp
  (eval-after-load "projectile"
    '(setq projectile-mode-line-lighter " Ⓟ"))
  (setq projectile-mode-line-lighter " Ⓟ")
  (projectile-global-mode)

#+end_src

** evgeni/minimap
#+begin_src emacs-lisp
(let ((base (current-buffer)))
  (setq ind (make-indirect-buffer
             base (concat "test"))))

(with-current-buffer ind
  (setq vertical-scroll-bar nil
        truncate-lines t
        buffer-read-only t
        mode-line-format (and mode-line-format "")
        minimap-active-overlay (make-overlay 0 0))
  (overlay-put minimap-active-overlay 
               'face sublimity-map-active-region)
  (text-scale-set sublimity-map-text-scale))
#+end_src


#+begin_src emacs-lisp
(defun evgeni/minimap-update (&optional window start)
  "Sync window informations to minimap."
  (interactive)
  (let ((basewin (get-buffer-window (current-buffer )))
        (miniwin (get-buffer-window ind))
        (minibuf ind))
    (let ((region-beg (window-start basewin))
          (region-end (window-end basewin t))
          (point (point)))
      (with-selected-window miniwin
        (set-window-buffer nil minibuf t)
        (with-current-buffer minibuf
          (goto-char point)
          (recenter)
          (move-overlay minimap-active-overlay
                         region-beg region-end) )))))
#+end_src

* Customization
** Common
*** package mode
#+begin_src emacs-lisp
(defadvice list-packages (after evgeni/list-packages-advice activate)
  (hl-line-mode 1))
#+end_src

** Functions
*** Swap window buffer
https://gist.github.com/mariusaeriksen/287633
   #+BEGIN_SRC emacs-lisp
     (defun evgeni/swap-window-buffer (number)
       (interactive)
       (let ((other-window (nth (1- number) (window-number-list))))
         (when other-window
           (let* ((this-window (selected-window))
                  (this-buffer (window-buffer this-window))
                  (other-buffer (window-buffer other-window))
                  (this-start (window-start this-window))
                  (other-start (window-start other-window)))
             (set-window-buffer this-window other-buffer)
             (set-window-buffer other-window this-buffer)
             (set-window-start this-window other-start)
             (set-window-start other-window this-start) 
             (select-window other-window)))))
     
   #+END_SRC

*** eval-and-replace
#+begin_src emacs-lisp
(defun eval-and-replace ()
  "Replace the preceding sexp with its value."
  (interactive)
  (backward-kill-sexp)
  (condition-case nil
      (prin1 (eval (read (current-kill 0)))
             (current-buffer))
    (error (message "Invalid expression")
           (insert (current-kill 0)))))
#+end_src

*** open-line (above/below)
http://emacsredux.com/blog/2013/06/15/open-line-above/

#+begin_src emacs-lisp
(defun smart-open-line ()
  "Insert an empty line after the current line.
Position the cursor at its beginning, according to the current mode."
  (interactive)
  (move-end-of-line nil)
  (newline-and-indent))

(defun smart-open-line-above ()
  "Insert an empty line above the current line.
Position the cursor at it's beginning, according to the current mode."
  (interactive)
  (move-beginning-of-line nil)
  (newline-and-indent)
  (forward-line -1)
  (indent-according-to-mode))

#+end_src

*** evgeni/show-filename
http://steckerhalter.co.vu/
#+begin_src emacs-lisp
(defun evgeni/show-file-name ()
  "Show the full path file name in the minibuffer."
  (interactive)
  (message (buffer-file-name))
  (kill-new (file-truename buffer-file-name)))
#+end_src

*** evgeni/switch-to-minibuffer-window
http://steckerhalter.co.vu/
#+begin_src emacs-lisp
(defun evgeni/switch-to-minibuffer-window ()
  "Switch to minibuffer window (if active)."
  (interactive)
  (when (active-minibuffer-window)
    (select-window (active-minibuffer-window))))
#+end_src

*** copy-line 
#+begin_src emacs-lisp
(defun evgeni/copy-line (arg)
  "Copy lines (as many as prefix argument) in the kill ring"
  (interactive "p")
  (kill-ring-save (line-beginning-position)
                  (line-beginning-position (+ 1 arg))))
#+end_src
 
*** hs-toggle-all
http://sachachua.com/blog/2006/10/emacs-hideshow/
#+begin_src emacs-lisp
(defun evgeni/hs-toggle-all ()
  "Toggle hideshow all."
  (interactive)
  (set (make-variable-buffer-local 'evgeni/hs-hide) (not evgeni/hs-hide))
  (if evgeni/hs-hide
      (hs-hide-all)
    (hs-show-all)))
#+end_src

** Options
#+begin_src emacs-lisp
(setq backup-directory-alist `((".*" . ,temporary-file-directory)))
(setq auto-save-file-name-transforms `((".*" ,temporary-file-directory t)))

(add-hook 'after-save-hook
          'executable-make-buffer-file-executable-if-script-p)
(setq set-mark-command-repeat-pop t)

(setq-default indent-tabs-mode nil)
(setq tab-width 4)

(setq sentence-end-double-space nil)
(setq scroll-preserve-screen-position t)
(setq isearch-allow-scroll t)

(delete-selection-mode 1)

(setq fill-column 80)

(global-subword-mode 1)

(setq default-major-mode 'org-mode)

(put 'upcase-region 'disabled nil)
(put 'narrow-to-page 'disabled nil)
(put 'narrow-to-region 'disabled nil)
(put 'set-goal-column 'disabled nil)

(tool-bar-mode -1)
(menu-bar-mode -1)
(scroll-bar-mode -1)
(blink-cursor-mode -1)  
#+END_SRC

** Mode line
*** format
#+begin_src emacs-lisp
(setq-default mode-line-format 
              '("%e "
                (:eval (propertize (number-to-string (window-number))
                                   'face 'helm-source-header))

                " "
                (:eval (propertize "%02l"
                                   'face 'font-lock-preprocessor-face)) ","
                (:eval (propertize "%02c"
                                   'face 'font-lock-preprocessor-face)) 
                " "
                (:eval (propertize "%p"))
                " "

                mode-line-mule-info
                (:eval (when overwrite-mode 
                         (propertize "O" 
                             'face 'font-lock-preprocessor-face
                             'help-echo (concat "Buffer is in "
                                                (if overwrite-mode "overwrite" "insert") " mode"))))

                (:eval (when (buffer-modified-p)
                         (propertize "X"
                                     'face 'font-lock-warning-face
                                     'help-echo "Buffer has been modified")))

                (:eval (when buffer-read-only
                         (propertize "R"
                                     'face 'font-lock-type-face
                                     'help-echo "Buffer is read-only")))  

                (:eval (when (and (buffer-file-name) (file-remote-p (buffer-file-name)))
                         (propertize "@"
                                     'face 'font-lock-type-face
                                     'help-echo "Buffer is read-only")))  
                " "
                (:eval (propertize "%b "
                                   'face 'font-lock-keyword-face))

                smartrep-mode-line-string
                mode-line-modes 
                mode-line-misc-info 
                mode-line-end-spaces

                (:eval (when vc-mode (propertize vc-mode  
                                   'face 'font-lock-string-face)))))
#+end_src

*** diminish
   #+begin_src emacs-lisp
(eval-after-load "auto-complete"
  '(diminish 'auto-complete-mode " Ⓐ"))
(eval-after-load "auto-highlight-symbol"
  '(diminish 'auto-highlight-symbol-mode " Ⓗ"))
(eval-after-load "eproject"
  '(diminish 'eproject-mode " eⓅ"))
(eval-after-load "flyspell"
  '(diminish 'flyspell-mode " Ⓢ"))
(eval-after-load "smartparens"
  '(diminish 'smartparens-mode " (Ⓢ)"))
(eval-after-load "paredit"
  '(diminish 'paredit-mode " (Ⓟ)"))
(eval-after-load "tagedit"
  '(diminish 'tagedit-mode " Ⓣ"))

(eval-after-load "yasnippet"
  '(diminish 'yas-minor-mode))

(eval-after-load "undo-tree"
  '(diminish 'undo-tree-mode))

(eval-after-load "helm"
  '(diminish 'helm-mode))

(eval-after-load "highlight-parentheses"
  '(diminish 'highlight-parentheses-mode))

(eval-after-load "drag-stuff"
  '(diminish 'drag-stuff-mode))

(eval-after-load "guide-key" 
  '(diminish 'guide-key-mode))   

(eval-after-load "back-button"
  '(diminish 'back-button-mode))

(eval-after-load "volatile-highlights"
  '(diminish 'volatile-highlights-mode))

(eval-after-load "anzu"
  '(diminish 'anzu-mode))
   #+END_SRC

*** frame-title
#+begin_src emacs-lisp
(setq frame-title-format "%b")
#+end_src

** dired
#+begin_src emacs-lisp
(require 'dired)
(setq dired-dwim-target t)
(setq dired-recursive-deletes t)
(setq dired-recursive-copies t) 
(setq wdired-allow-to-change-permissions t)
(define-key dired-mode-map (kbd "<return>") 'dired-find-alternate-file) ; was dired-advertised-find-file
(define-key dired-mode-map (kbd "^") (lambda () (interactive) (find-alternate-file "..")))  ; was dired-up-directory
(add-hook 'dired-mode-hook (lambda () "setup dired"
                             (hl-line-mode 1)
                             (evgeni/set-key "C-o" nil)))
#+end_src

** recentf (recent files)
http://www.masteringemacs.org/articles/2011/01/27/find-files-faster-recent-files-package/
   #+BEGIN_SRC emacs-lisp
(setq recentf-max-saved-items 50)
   #+END_SRC

** hippie-expand
   #+BEGIN_SRC emacs-lisp
     (setq hippie-expand-try-functions-list '(yas-hippie-try-expand
 					      try-expand-dabbrev
					      try-expand-dabbrev-from-kill
					      try-complete-file-name-partially
					      try-complete-file-name
					      try-expand-dabbrev-all-buffers
					      try-expand-all-abbrevs
					      try-expand-list
					      try-expand-line))
   #+END_SRC

** uniquify
#+begin_src emacs-lisp
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'post-forward-angle-brackets)
#+end_src

** Other
#+begin_src emacs-lisp
(global-undo-tree-mode)

(global-smartscan-mode 1)

(require 'volatile-highlights)
(volatile-highlights-mode t)

(setq initial-scratch-message ";; Use the source Luke !!! \n")
(setq browse-url-generic-program (executable-find "conkeror"))
#+end_src

* Key bindings
Use a minor mode for custom key bindings. This allows to override other minor mode
key bindings and also allow to more easily lookup custom bindigs (describe mode ...).

#+begin_src emacs-lisp
  (defvar evgeni/keys-minor-mode-map (make-keymap) "evgeni/keys-minor-mode keymap.")
  
  (define-minor-mode evgeni/keys-minor-mode
    "A minor mode so that my key settings override annoying major modes."
    t " evgeni/keys" 'evgeni/keys-minor-mode-map)
 
  (evgeni/keys-minor-mode 1)
  (diminish 'evgeni/keys-minor-mode)
  
  (add-hook 'minibuffer-setup-hook '(lambda () (evgeni/keys-minor-mode 0)))
#+end_src

** Macros
 #+BEGIN_SRC emacs-lisp 
(defmacro evgeni/set-key (key &rest body)
  `(define-key evgeni/keys-minor-mode-map (kbd ,key) ,@body))

(defmacro evgeni/add-binding (key name &rest body)
  `(progn
     (defun ,name ()
       (interactive)
       ,@body)
     (evgeni/set-key (kbd ,key)
                 ',name)))

(defmacro evgeni/add-smartrep-bindings (prefix &rest body)
  `(smartrep-define-key evgeni/keys-minor-mode-map
       (kbd ,prefix) '(,@body)))
 #+end_src

** Completion
#+begin_src emacs-lisp
(evgeni/set-key "<C-tab>" 'auto-complete)
(evgeni/set-key "<C-M-tab>" 'evgeni/ac-complete-with-helm)
 #+end_src
 
** Re-definitions
#+begin_src emacs-lisp
(global-unset-key (kbd "C-z"))
(global-unset-key (kbd "C-x C-c"))
(evgeni/set-key "C-z C-z" 'global-set-key)
(evgeni/set-key "C-z C-l" 'local-set-key)

(evgeni/set-key "<s-SPC>" 'just-one-space)
(evgeni/set-key "<M-SPC>" 'set-mark-command)

(evgeni/set-key "C-o" 'yas/expand)
(evgeni/set-key "M-/" 'hippie-expand)

(evgeni/set-key "M-o" 'smart-open-line)
(evgeni/set-key "M-O" 'smart-open-line-above)
(evgeni/set-key "C-c M-s" 'center-line)
(evgeni/set-key "C-c M-S" 'center-paragraph)


(evgeni/set-key "M-0" 'ace-jump-mode) 
(evgeni/set-key "M-i" 'jump-char-forward)
(evgeni/set-key "M-I" 'jump-char-backward)
(evgeni/set-key "C-=" 'er/expand-region)
(evgeni/set-key "C-+" 'er/contract-region)

(evgeni/set-key "C-x k" 'kill-this-buffer)
(evgeni/set-key "C-x j" 'direx:jump-to-directory-other-window)

(evgeni/set-key "C-h a" 'helm-apropos)
(evgeni/set-key "C-x C-b" 'helm-buffers-list)
(evgeni/set-key "M-x" 'helm-M-x)
(evgeni/set-key "C-x C-f" 'helm-find-files)
#+end_src

** Common
 #+begin_src emacs-lisp
(evgeni/set-key "M-W" 'evgeni/copy-line)

(evgeni/set-key "M-z" 'zap-up-to-char)
(evgeni/set-key "M-Z" 'zap-to-char)

(evgeni/set-key "C-c e" 'eval-and-replace)
(evgeni/set-key "C-c F" 'evgeni/show-file-name)

(evgeni/set-key "C-c o l" 'org-store-link)
(evgeni/set-key "C-c o c" 'org-capture)
(evgeni/set-key "C-c o a" 'org-agenda)
(evgeni/set-key "C-c o b" 'org-iswitchb)

(evgeni/set-key "C-c R" 
            '(lambda () 
               (interactive)
               (load-file 
                (expand-file-name "~/.emacs.d/init.el"))))

(evgeni/set-key "C-c l" 'slime-selector)

(evgeni/set-key "C-c f" 'ffap)

;; rosemacs
(when (boundp 'ros-keymap)
  (evgeni/set-key "C-c r" ros-keymap))

 #+END_SRC

** Toggle
*** Common
#+begin_src emacs-lisp
(evgeni/add-binding "C-c t t" evgeni/toggle-terminal (shell-pop nil))

(evgeni/set-key "C-c t a" 'auto-revert-mode)
(evgeni/set-key "C-c t b" 'sr-speedbar-toggle)
(evgeni/set-key "C-c t c" 'flycheck-mode)
(evgeni/set-key "C-c t h" 'hs-minor-mode)
(evgeni/set-key "C-c t l" 'scroll-lock-mode)
(evgeni/set-key "C-c t m" 'sublimity-mode)
(evgeni/set-key "C-c t s" 'flyspell-mode)
(evgeni/set-key "C-c t v" 'view-mode)
 #+end_src
 
*** Interface
#+begin_src emacs-lisp
(evgeni/set-key "C-c i b" 'toggle-tool-bar-mode-from-frame)
(evgeni/set-key "C-c i f" 'auto-fill-mode)
(evgeni/set-key "C-c i g" 'golden-ratio-mode)
(evgeni/set-key "C-c i l" 'linum-mode)
(evgeni/set-key "C-c i m" 'toggle-menu-bar-mode-from-frame)
(evgeni/set-key "C-c i s" 'toggle-scroll-bar)
(evgeni/set-key "C-c i t" 'tabbar-mode)
(evgeni/set-key "C-c i v" 'visual-line-mode)
(evgeni/set-key "C-c i w" 'toggle-truncate-lines)
 #+end_src
 
*** ECB
#+begin_src emacs-lisp
      (setq evgeni/ecb-active-p nil)
      (add-hook 'ecb-activate-hook #'(lambda () (setq evgeni/ecb-active-p t)))
      (add-hook 'ecb-deactivate-hook #'(lambda () (setq evgeni/ecb-active-p nil)))
      
      (evgeni/add-binding "C-c t E" evgeni/toggle-ecb
                      (if evgeni/ecb-active-p
                          (command-execute 'ecb-deactivate)
                        (command-execute 'ecb-activate)))
 #+end_src

** Major-modes
#+begin_src emacs-lisp
(evgeni/set-key "C-c m m" 'helm-enable-minor-mode)
(evgeni/set-key "C-c m M" 'helm-disable-minor-mode)

(evgeni/set-key "C-c m g" 'magit-status)

(evgeni/set-key "C-c m e" 'ebib)

(evgeni/set-key "C-c m s" 'eshell)
#+end_src

** Helm bindings
 #+begin_src emacs-lisp
(evgeni/set-key "C-c G" 'helm-google-suggest)
(evgeni/set-key "C-c g" 'helm-google)

(evgeni/set-key "C-c h A" 'helm-ag-r)
(evgeni/set-key "C-c h B" 'helm-bibtex)
(evgeni/set-key "C-c h C" 'helm-colors)
(evgeni/set-key "C-c h F" 'helm-find)
(evgeni/set-key "C-c h G" 'helm-git-grep)
(evgeni/set-key "C-c h L" 'helm-locate-library)
(evgeni/set-key "C-c h M" 'helm-descbinds)
(evgeni/set-key "C-c h O" 'helm-multi-occur)
(evgeni/set-key "C-c h P" 'helm-browse-project)
(evgeni/set-key "C-c h R" 'helm-resume)
(evgeni/set-key "C-c h S" 'math-symbols-helm)
(evgeni/set-key "C-c h T" 'helm-themes)
(evgeni/set-key "C-c h a" 'helm-ag)
(evgeni/set-key "C-c h b" 'helm-bm)
(evgeni/set-key "C-c h c" 'helm-flycheck)
(evgeni/set-key "C-c h d" 'helm-dash)
(evgeni/set-key "C-c h f" 'helm-for-files) 
(evgeni/set-key "C-c h g" 'helm-do-grep)
(evgeni/set-key "C-c h h" 'helm-org-headlines)
(evgeni/set-key "C-c h i" 'helm-imenu)
(evgeni/set-key "C-c h k" 'helm-show-kill-ring)
(evgeni/set-key "C-c h l" 'helm-locate)
(evgeni/set-key "C-c h m" 'helm-all-mark-rings)
(evgeni/set-key "C-c h o" 'helm-occur)
(evgeni/set-key "C-c h p" 'helm-projectile)
(evgeni/set-key "C-c h r" 'helm-register)
(evgeni/set-key "C-c h s" 'helm-semantic)
(evgeni/set-key "C-c h w" 'helm-spaces)
(evgeni/set-key "C-c h x" 'helm-regexp)
(evgeni/set-key "C-c h y" 'helm-yas-complete)
(evgeni/set-key "C-c h 8" 'helm-ucs)

; info commands
(evgeni/set-key "C-c h I e" 'helm-info-emacs)
(evgeni/set-key "C-c h I o" 'helm-orgcard)
(evgeni/set-key "C-c h I p" 'helm-pydoc)
 #+end_src
 
** Modes
*** Window management
#+begin_src emacs-lisp
(evgeni/add-smartrep-bindings "C-c w"
                          ("w" . (winner-undo))
                          ("W" . (winner-redo)))
(evgeni/set-key "C-c w t" 'transpose-frame)
(evgeni/set-key "C-c w m" 'evgeni/switch-to-minibuffer-window)
(evgeni/set-key "C-c w d" 'dedicated-mode)

(evgeni/add-smartrep-bindings "C-c w c"
                          ("i" . (command-execute 'enlarge-window))
                          ("k" . (command-execute 'shrink-window))
                          ("l" . (command-execute 'enlarge-window-horizontally))
                          ("j" . (command-execute 'shrink-window-horizontally))
                          ("o" . (command-execute 'other-window))
                          ("O" . (command-execute 'other-window '-)))
#+end_src

*** goto-last-change
#+begin_src emacs-lisp
(evgeni/set-key "s-/" 'goto-last-change)
(evgeni/set-key "s-?" 'goto-last-change-reverse)
#+end_src

*** evgeni/swap-window bindings 
 #+begin_src emacs-lisp
   (evgeni/set-key "C-M-1" '(lambda () "move buffer to window 1" (interactive) (evgeni/swap-window-buffer 1)))
   (evgeni/set-key "C-M-2" '(lambda () "move buffer to window 2" (interactive) (evgeni/swap-window-buffer 2)))
   (evgeni/set-key "C-M-3" '(lambda () "move buffer to window 3" (interactive) (evgeni/swap-window-buffer 3)))
   (evgeni/set-key "C-M-4" '(lambda () "move buffer to window 4" (interactive) (evgeni/swap-window-buffer 4)))
   (evgeni/set-key "C-M-5" '(lambda () "move buffer to window 5" (interactive) (evgeni/swap-window-buffer 5)))
   (evgeni/set-key "C-M-6" '(lambda () "move buffer to window 6" (interactive) (evgeni/swap-window-buffer 6)))
   (evgeni/set-key "C-M-7" '(lambda () "move buffer to window 7" (interactive) (evgeni/swap-window-buffer 7)))
   (evgeni/set-key "C-M-8" '(lambda () "move buffer to window 8" (interactive) (evgeni/swap-window-buffer 8)))
   (evgeni/set-key "C-M-9" '(lambda () "move buffer to window 9" (interactive) (evgeni/swap-window-buffer 9)))
 #+end_src

*** drag-stuff
#+begin_src emacs-lisp
  (setq drag-stuff-modifier '(meta super))
  (drag-stuff-global-mode t)
#+end_src

*** visual-regexp
#+begin_src emacs-lisp
(evgeni/set-key "C-c x x" 'vr/replace)
(evgeni/set-key "C-c x q" 'vr/query-replace)
;; if you use multiple-cursors, this is for you:
(evgeni/set-key "C-c x m" 'vr/mc-mark)
#+end_src

*** bm (visual bookmarsk)
#+begin_src emacs-lisp
(evgeni/set-key "C-c b b" 'bm-toggle)
(evgeni/set-key "C-c b s" 'bm-toggle-buffer-persistence)

(evgeni/add-smartrep-bindings "C-c b"
                          ("n" . (bm-next))
                          ("p" . (bm-previous)))

#+end_src

*** smartscan 
#+begin_src emacs-lisp
  (define-key smartscan-map (kbd "M-p") nil)
  (define-key smartscan-map (kbd "M-n") nil)
  
  (define-key smartscan-map (kbd "M-s-p") 'smartscan-symbol-go-backward)
  (define-key smartscan-map (kbd "M-s-n") 'smartscan-symbol-go-forward)
#+end_src

*** multiple-cursors
#+begin_src emacs-lisp
(evgeni/set-key  "C-c _" 'mc/edit-lines)

(evgeni/set-key  "C->" 'mc/mark-next-like-this)
(evgeni/set-key  "C-<" 'mc/mark-previous-like-this)
(evgeni/set-key  "C-c C-<" 'mc/mark-all-like-this)
#+end_src

*** smartparens
#+begin_src emacs-lisp
(define-key sp-keymap (kbd "C-M-f") 'sp-forward-sexp)
(define-key sp-keymap (kbd "C-M-b") 'sp-backward-sexp)
(define-key sp-keymap (kbd "C-M-n") 'sp-next-sexp)
(define-key sp-keymap (kbd "C-M-p") 'sp-previous-sexp)
(define-key sp-keymap (kbd "C-M-u") 'sp-backward-up-sexp)
(define-key sp-keymap (kbd "C-M-d") 'sp-down-sexp)
(define-key sp-keymap (kbd "C-M-t") 'sp-transpose-sexp)

(define-key sp-keymap (kbd "C-M-k") 'sp-kill-sexp)

(define-key sp-keymap (kbd "C-M-e") 'sp-up-sexp)
(define-key sp-keymap (kbd "C-M-a") 'sp-backward-down-sexp)

(define-key sp-keymap (kbd "C-)") 'sp-forward-slurp-sexp)
(define-key sp-keymap (kbd "C-(") 'sp-backward-slurp-sexp)
(define-key sp-keymap (kbd "C-M-)") 'sp-forward-barf-sexp)
(define-key sp-keymap (kbd "C-M-(") 'sp-backward-barf-sexp)

(define-key sp-keymap (kbd "M-F") 'sp-forward-symbol)
(define-key sp-keymap (kbd "M-B") 'sp-backward-symbol)

(define-key sp-keymap (kbd "C-c s M-w") 'sp-copy-sexp)

(define-key sp-keymap (kbd "C-c s u") 'sp-unwrap-sexp)
(define-key sp-keymap (kbd "C-c s U") 'sp-backward-unwrap-sexp)

(define-key sp-keymap (kbd "C-c s s") 'sp-splice-sexp)
(define-key sp-keymap (kbd "C-c s F") 'sp-splice-sexp-killing-forward)
(define-key sp-keymap (kbd "C-c s B") 'sp-splice-sexp-killing-backward)
(define-key sp-keymap (kbd "C-c s r") 'sp-splice-sexp-killing-around)

(define-key sp-keymap (kbd "C-]") 'sp-select-next-thing-exchange)
(define-key sp-keymap (kbd "C-}") 'sp-select-previous-thing)
(define-key sp-keymap (kbd "C-M-]") 'sp-select-next-thing)
 
(define-key sp-keymap (kbd "<s-t>") 'sp-prefix-tag-object)
(define-key sp-keymap (kbd "<s-p>") 'sp-prefix-pair-object)
(define-key sp-keymap (kbd "C-c s c") 'sp-convolute-sexp)
(define-key sp-keymap (kbd "C-c s a") 'sp-absorb-sexp)
(define-key sp-keymap (kbd "C-c s e") 'sp-emit-sexp)
(define-key sp-keymap (kbd "C-c s p") 'sp-add-to-previous-sexp)
(define-key sp-keymap (kbd "C-c s n") 'sp-add-to-next-sexp)
(define-key sp-keymap (kbd "C-c s j") 'sp-join-sexp)
(define-key sp-keymap (kbd "C-c s S") 'sp-split-sexp)

(define-key sp-keymap (kbd "C-c s k") 'sp-kill-hybrid-sexp)
#+end_src

*** howdoi
#+begin_src emacs-lisp
(evgeni/set-key "C-c q" (lambda () (interactive) (command-execute 'howdoi-query)))
(evgeni/set-key "C-c Q" (lambda () (interactive) (command-execute 'howdoi-query-line-at-point)))
#+end_src

*** hide-show
#+begin_src emacs-lisp
(add-hook 'hs-minor-mode-hook
          '(lambda ()
             (define-key hs-minor-mode-map (kbd "<s-tab>") 'hs-toggle-hiding)
             (define-key hs-minor-mode-map (kbd "<S-s-iso-lefttab>") 'evgeni/hs-toggle-all)))
#+end_src

** Local
*** org-mode
#+begin_src emacs-lisp
(add-hook 'org-mode-hook 
          (lambda () "DOCSTRING"   
            (local-set-key (kbd "C-z s") 'org-screenshot)))
#+end_src

* TODO Work to do

** webjump

* Inactive
:PROPERTIES:
:tangle: no
:END:
** i3-emacs
#+begin_src emacs-lisp
    (add-to-list 'load-path 
                 (expand-file-name "~/.emacs.d/lisp/i3-emacs"))
  
  (require 'i3-integration)
  (i3-one-window-per-frame-mode-on)
#+end_src

*
** Company-mode
#+begin_src emacs-lisp
(add-hook 'after-init-hook 'global-company-mode)
#+end_src

** Libre fix (remove T1 fontenc)
#+begin_src emacs-lisp
  (eval-after-load "org"
    '(setq org-latex-default-packages-alist
          (remove '("T1" "fontenc" t) org-latex-default-packages-alist)))
#+end_src

** mode line names
   #+BEGIN_SRC emacs-lisp
(add-hook 'emacs-lisp-mode-hook
          (lambda () (setq mode-name "Elisp")))

(add-hook 'python-mode-hook
          (lambda () (setq mode-name "Python")))

   #+end_src

** paredit
   #+BEGIN_SRC emacs-lisp
(autoload 'enable-paredit-mode "paredit" "Turn on pseudo-structural editing of Lisp code." t)
(add-hook 'emacs-lisp-mode-hook       #'enable-paredit-mode)
(add-hook 'eval-expression-minibuffer-setup-hook #'enable-paredit-mode)
(add-hook 'ielm-mode-hook             #'enable-paredit-mode)
(add-hook 'lisp-mode-hook             #'enable-paredit-mode)
(add-hook 'lisp-interaction-mode-hook #'enable-paredit-mode)
(add-hook 'scheme-mode-hook           #'enable-paredit-mode)
(add-hook 'slime-repl-mode-hook       #'enable-paredit-mode)
   #+END_SRC

** god-mode
#+begin_src emacs-lisp
(defun evgeni/update-cursor ()
  (setq cursor-type (if (or god-local-mode buffer-read-only)
                        'box
                      'bar)))

(add-hook 'god-mode-enabled-hook 'evgeni/update-cursor)
(add-hook 'god-mode-disabled-hook 'evgeni/update-cursor)
#+end_src

